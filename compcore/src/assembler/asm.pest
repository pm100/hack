program = _{ SOI ~ line* ~ EOI }
line    = _{ stmt ~ NEWLINE? }
stmt    = _{ a_inst | c_inst | l_inst | directive | d_inst | comment }
label   = @{ (ASCII_ALPHA) ~ (ASCII_ALPHA | ASCII_DIGIT | "." | "_" | "$")* }
dest    =  { "AMD" | "MD" | "AM" | "AD" | "A" | "M" | "D" }
comp    =  {
    "0"
  | "1"
  | "-1"
  | "M+1"
  | "M-1"
  | "D+M"
  | "D-M"
  | "M-D"
  | "D&M"
  | "D|M"
  | "D+1"
  | "A+1"
  | "D-1"
  | "A-1"
  | "D+A"
  | "D-A"
  | "A-D"
  | "D&A"
  | "D|A"
  | "D"
  | "A"
  | "!D"
  | "!A"
  | "-D"
  | "-A"
  | "M"
  | "!M"
  | "-M"
}
jump    =  { "JGT" | "JEQ" | "JGE" | "JLT" | "JNE" | "JLE" | "JMP" }

a_inst    =  { "@" ~ (hex_num | int | label) }
c_inst    =  { (dest ~ "=")? ~ comp ~ (";" ~ jump)? }
l_inst    =  { "(" ~ label ~ ")" }
d_inst    =  { word_inst }
word_inst =  { word ~ (hex_num | int) }
word      = _{ ^"word" }
comment   =  { "//" ~ (!"\n" ~ ANY)* }
directive =  { dot_org }
dot_org   =  { ".org" ~ (hex_num | int) }

hex_num    = @{ ("0X" | "0x") ~ hex+ }
hex        = @{ '0'..'9' | 'a'..'f' | 'A'..'F' }
int        = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
WHITESPACE = _{ " " | "\t" | NEWLINE }
